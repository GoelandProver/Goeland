%------------------------------------------------------------------------------
% File     : MED001+0 : TPTP v8.1.2. Released v3.2.0.
% Domain   : Medicine
% Axioms   : Physiology Diabetes Mellitus type 2
% Version  : [HLB05] axioms : Especial.
% English  : Physiological mechanisms of diabetes mellitus type 2

% Refs     : [HLB05] Hommersom et al. (2005), Automated Theorem Proving for
%          : [Hom06] Hommersom (2006), Email to G. Sutcliffe
% Source   : [Hom06]
% Names    :

% Status   : Satisfiable
% Syntax   : Number of formulae    :   18 (   1 unt;   0 def)
%            Number of atoms       :   76 (   0 equ)
%            Maximal formula atoms :   11 (   4 avg)
%            Number of connectives :   95 (  37   ~;  12   |;  15   &)
%                                         (   0 <=>;  31  =>;   0  <=;   0 <~>)
%            Maximal formula depth :    9 (   6 avg)
%            Maximal term depth    :    1 (   1 avg)
%            Number of predicates  :   15 (  15 usr;   0 prp; 1-2 aty)
%            Number of functors    :    0 (   0 usr;   0 con; --- aty)
%            Number of variables   :   42 (  42   !;   0   ?)
% SPC      : 

% Comments :
%------------------------------------------------------------------------------
fof(irreflexivity_gt,axiom,
    ! [X] : ~ gt(X,X) ).

fof(transitivity_gt,axiom,
    ! [X,Y,Z] :
      ( ( gt(X,Y)
        & gt(Y,Z) )
     => gt(X,Z) ) ).

fof(xorcapacity1,axiom,
    ! [X0] :
      ( bcapacityne(X0)
      | bcapacityex(X0)
      | bcapacitysn(X0) ) ).

fof(xorcapacity2,axiom,
    ! [X0] :
      ( ~ bcapacityne(X0)
      | ~ bcapacityex(X0) ) ).

fof(xorcapacity3,axiom,
    ! [X0] :
      ( ~ bcapacityne(X0)
      | ~ bcapacitysn(X0) ) ).

fof(xorcapacity4,axiom,
    ! [X0] :
      ( ~ bcapacityex(X0)
      | ~ bcapacitysn(X0) ) ).

fof(xorcondition1,axiom,
    ! [X0] :
      ( conditionhyper(X0)
      | conditionhypo(X0)
      | conditionnormo(X0) ) ).

fof(xorcondition2,axiom,
    ! [X0] :
      ( ~ conditionhyper(X0)
      | ~ conditionhypo(X0) ) ).

fof(xorcondition3,axiom,
    ! [X0] :
      ( ~ conditionhyper(X0)
      | ~ conditionnormo(X0) ) ).

fof(xorcondition4,axiom,
    ! [X0] :
      ( ~ conditionhypo(X0)
      | ~ conditionnormo(X0) ) ).

fof(insulin_effect,axiom,
    ! [X0] :
      ( ! [X1] :
          ( ~ gt(X0,X1)
         => drugi(X1) )
     => ! [X1] :
          ( ~ gt(X0,X1)
         => ( uptakelg(X1)
            & uptakepg(X1) ) ) ) ).

fof(liver_glucose,axiom,
    ! [X0,X1] :
      ( ~ gt(X0,X1)
     => ( uptakelg(X1)
       => ~ releaselg(X1) ) ) ).

fof(sulfonylurea_effect,axiom,
    ! [X0] :
      ( ( ! [X1] :
            ( ~ gt(X0,X1)
           => drugsu(X1) )
        & ~ bcapacityex(X0) )
     => ! [X1] :
          ( ~ gt(X0,X1)
         => bsecretioni(X1) ) ) ).

fof(biguanide_effect,axiom,
    ! [X0] :
      ( ! [X1] :
          ( ~ gt(X0,X1)
         => drugbg(X1) )
     => ! [X1] :
          ( ~ gt(X0,X1)
         => ~ releaselg(X1) ) ) ).

fof(sn_cure_1,axiom,
    ! [X0] :
      ( ( ! [X1] :
            ( ~ gt(X0,X1)
           => bsecretioni(X1) )
        & bcapacitysn(X0)
        & qilt27(X0)
        & ! [X1] :
            ( gt(X0,X1)
           => conditionhyper(X1) ) )
     => ! [X1] :
          ( ~ gt(X0,X1)
         => conditionnormo(X1) ) ) ).

fof(sn_cure_2,axiom,
    ! [X0] :
      ( ( ! [X1] :
            ( ~ gt(X0,X1)
           => ~ releaselg(X1) )
        & bcapacitysn(X0)
        & ~ qilt27(X0)
        & ! [X1] :
            ( gt(X0,X1)
           => conditionhyper(X1) ) )
     => ! [X1] :
          ( ~ gt(X0,X1)
         => conditionnormo(X1) ) ) ).

fof(ne_cure,axiom,
    ! [X0] :
      ( ( ( ! [X1] :
              ( ~ gt(X0,X1)
             => ~ releaselg(X1) )
          | ! [X1] :
              ( ~ gt(X0,X1)
             => uptakepg(X1) ) )
        & bcapacityne(X0)
        & ! [X1] :
            ( ~ gt(X0,X1)
           => bsecretioni(X1) )
        & ! [X1] :
            ( gt(X0,X1)
           => conditionhyper(X1) ) )
     => ! [X1] :
          ( ~ gt(X0,X1)
         => conditionnormo(X1) ) ) ).

fof(ex_cure,axiom,
    ! [X0] :
      ( ( ! [X1] :
            ( ~ gt(X0,X1)
           => uptakelg(X1) )
        & ! [X1] :
            ( ~ gt(X0,X1)
           => uptakepg(X1) )
        & bcapacityex(X0)
        & ! [X1] :
            ( gt(X0,X1)
           => conditionhyper(X1) ) )
     => ! [X1] :
          ( ~ gt(X0,X1)
         => ( conditionnormo(X1)
            | conditionhypo(X1) ) ) ) ).

%------------------------------------------------------------------------------
