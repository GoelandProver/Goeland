%------------------------------------------------------------------------------
% File     : MVA001-0 : TPTP v8.1.2. Released v8.0.0.
% Domain   : MV-algebras
% Axioms   : Generalized MV algebras (equality)
% Version  : [Ver10] (equality) axioms.
% English  :

% Refs     : [GT05]  Galatos & Tsinakis (2005), Generalized MV-algebras
%          : [GJ+07] Galatos et al. (2007), Residuated Lattices: An Algebra
%          : [Ver10] Veroff (2010), Email to Geoff Sutcliffe
%          : [Sma21] Smallbone (2021), Email to Geoff Sutcliffe
% Source   : [Ver10]
% Names    : gmv+1.ax [Sma21]

% Status   : Satisfiable
% Syntax   : Number of clauses     :   18 (  18 unt;   0 nHn;   0 RR)
%            Number of literals    :   18 (  18 equ;   0 neg)
%            Maximal clause size   :    1 (   1 avg)
%            Maximal term depth    :    5 (   2 avg)
%            Number of predicates  :    1 (   0 usr;   0 prp; 2-2 aty)
%            Number of functors    :    7 (   7 usr;   1 con; 0-2 aty)
%            Number of variables   :   39 (   6 sgn)
% SPC      : CNF_SAT_RFO_PEQ_UEQ

% Comments :
%------------------------------------------------------------------------------
cnf(associativity_of_meet,axiom,
    ( meet(meet(X,Y),Z) = meet(X,meet(Y,Z)) )).

cnf(associativity_of_join,axiom,
    ( join(join(X,Y),Z) = join(X,join(Y,Z)) )).

cnf(idempotence_of_meet,axiom,
    ( meet(X,X) = X )).

cnf(idempotence_of_join,axiom,
    ( join(X,X) = X )).

cnf(commutativity_of_meet,axiom,
    ( meet(X,Y) = meet(Y,X) )).

cnf(commutativity_of_join,axiom,
    ( join(X,Y) = join(Y,X) )).

cnf(absorption_a,axiom,
    ( join(meet(X,Y),X) = X )).

cnf(absorption_b,axiom,
    ( meet(join(X,Y),X) = X )).

cnf(residual_a,axiom,
    ( join(op(X,meet(ld(X,Z),Y)),Z) = Z )).

cnf(residual_b,axiom,
    ( join(op(meet(Y,rd(Z,X)),X),Z) = Z )).

cnf(residual_c,axiom,
    ( meet(ld(X,join(op(X,Y),Z)),Y) = Y )).

cnf(residual_d,axiom,
    ( meet(rd(join(op(Y,X),Z),X),Y) = Y )).

cnf(monoid_associativity,axiom,
    ( op(op(X,Y),Z) = op(X,op(Y,Z)) )).

cnf(left_monoid_unit,axiom,
    ( op(unit,X) = X )).

cnf(right_monoid_unit,axiom,
    ( op(X,unit) = X )).

cnf(generalized_mv_algebra_a,axiom,
    ( join(X,Y) = rd(X,ld(join(X,Y),X)) )).

cnf(generalized_mv_algebra_b,axiom,
    ( join(X,Y) = ld(rd(X,join(X,Y)),X) )).

cnf(definition_of_at,axiom,
    ( at(X,Y) = op(op(X,ld(X,unit)),ld(ld(Y,unit),unit)) )).

%------------------------------------------------------------------------------
