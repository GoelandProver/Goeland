%------------------------------------------------------------------------------
% File     : LCL013^6 : TPTP v8.1.2. Released v4.0.0.
% Domain   : Logic Calculi (Modal logic)
% Axioms   : Modal logic S5
% Version  : [Ben09] axioms.
% English  : Embedding of monomodal logic S5 in simple type theory.

% Refs     : [Ben09] Benzmueller (2009), Email to Geoff Sutcliffe
% Source   : [Ben09]
% Names    :

% Status   : Satisfiable
% Syntax   : Number of formulae    :    8 (   2 unt;   3 typ;   2 def)
%            Number of atoms       :   14 (   2 equ;   0 cnn)
%            Maximal formula atoms :    2 (   1 avg)
%            Number of connectives :   11 (   1   ~;   1   |;   0   &;   9   @)
%                                         (   0 <=>;   0  =>;   0  <=;   0 <~>)
%            Maximal formula depth :    2 (   2 avg;   9 nst)
%            Number of types       :    2 (   0 usr)
%            Number of type conns  :   10 (  10   >;   0   *;   0   +;   0  <<)
%            Number of symbols     :    8 (   7 usr;   4 con; 0-2 aty)
%            Number of variables   :    4 (   3   ^   1   !;   0   ?;   4   :)
% SPC      : 

% Comments : Requires LCL013^0
%------------------------------------------------------------------------------
%----We reserve an accessibility relation constant rel_s5
thf(rel_s5_type,type,
    rel_s5: $i > $i > $o ).

%----We define mbox_s5 and mdia_s5 based on rel_s5
thf(mbox_s5_type,type,
    mbox_s5: ( $i > $o ) > $i > $o ).

thf(mbox_s5,definition,
    ( mbox_s5
    = ( ^ [Phi: $i > $o,W: $i] :
        ! [V: $i] :
          ( ~ ( rel_s5 @ W @ V )
          | ( Phi @ V ) ) ) ) ).

thf(mdia_s5_type,type,
    mdia_s5: ( $i > $o ) > $i > $o ).

thf(mdia_s5,definition,
    ( mdia_s5
    = ( ^ [Phi: $i > $o] : ( mnot @ ( mbox_s5 @ ( mnot @ Phi ) ) ) ) ) ).

%----We have now two options for stating the B conditions: 
%----We can (i) directly formulate conditions for the accessibility relation
%----constant or we can (ii) state corresponding axioms. We here prefer (i)
thf(a1,axiom,
    mreflexive @ rel_s5 ).

thf(a2,axiom,
    mtransitive @ rel_s5 ).

thf(a3,axiom,
    msymmetric @ rel_s5 ).

%------------------------------------------------------------------------------
