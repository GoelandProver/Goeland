%--------------------------------------------------------------------------
% File     : HEN003-0 : TPTP v8.1.2. Released v1.0.0.
% Domain   : Henkin Models
% Axioms   : Henkin model (equality) axioms
% Version  : [MOW76] (equality) axioms :
%            Reduced > Complete.
% English  :

% Refs     : [MOW76] McCharen et al. (1976), Problems and Experiments for a
% Source   : [ANL]
% Names    :

% Status   : Satisfiable
% Syntax   : Number of clauses     :    5 (   4 unt;   0 nHn;   1 RR)
%            Number of literals    :    7 (   7 equ;   2 neg)
%            Maximal clause size   :    3 (   1 avg)
%            Maximal term depth    :    4 (   1 avg)
%            Number of predicates  :    1 (   0 usr;   0 prp; 2-2 aty)
%            Number of functors    :    3 (   3 usr;   2 con; 0-2 aty)
%            Number of variables   :    9 (   3 sgn)
% SPC      : 

% Comments : less_equal replaced by divides
%--------------------------------------------------------------------------
%----A0: Definition of less_equal, used to replace all occurrences
%----of less_equal(x,y)
%----    --less_equal(x,y) | (divide(x,y) = zero).
%----    (divide(x,y) != zero) | ++less_equal(x,y).

%----A1: x/y <= x
cnf(quotient_smaller_than_numerator,axiom,
    divide(divide(X,Y),X) = zero ).

%----A2: (x/z) / (y/z) <= (x/y) / z
cnf(quotient_property,axiom,
    divide(divide(divide(X,Z),divide(Y,Z)),divide(divide(X,Y),Z)) = zero ).

%----A3: 0<=x  NOTE: this axiom is dependant
cnf(zero_is_smallest,axiom,
    divide(zero,X) = zero ).

%----A4: x <= y and y <= x implies that x = y
cnf(divide_and_equal,axiom,
    ( divide(X,Y) != zero
    | divide(Y,X) != zero
    | X = Y ) ).

%----A5: x <= 1 (Thus an implicative model with unit )
cnf(identity_is_largest,axiom,
    divide(X,identity) = zero ).

%----Implicit in equality formulation: '/' is well defined

%--------------------------------------------------------------------------
