%--------------------------------------------------------------------------
% File     : TOP001-0 : TPTP v8.1.2. Released v1.0.0.
% Domain   : Topology (Point set)
% Axioms   : Point-set topology
% Version  : [WM89] axioms : Incomplete.
% English  :

% Refs     : [WM89]  Wick & McCune (1989), Automated Reasoning about Elemen
% Source   : [WM89]
% Names    :

% Status   : Satisfiable
% Syntax   : Number of clauses     :  109 (   0 unt;  23 nHn; 104 RR)
%            Number of literals    :  336 (   0 equ; 205 neg)
%            Maximal clause size   :    8 (   3 avg)
%            Maximal term depth    :    3 (   1 avg)
%            Number of predicates  :   22 (  22 usr;   0 prp; 1-4 aty)
%            Number of functors    :   35 (  35 usr;   1 con; 0-5 aty)
%            Number of variables   :  357 (  56 sgn)
% SPC      : 

% Comments : These axioms are incomplete, in that they do not contain the
%            requisite set theory axioms. Problems that use this axiom set
%            must supply appropriate set theory axioms.
%--------------------------------------------------------------------------
%----Sigma (union of members).
cnf(union_of_members_1,axiom,
    ( ~ element_of_set(U,union_of_members(Vf))
    | element_of_set(U,f1(Vf,U)) ) ).

cnf(union_of_members_2,axiom,
    ( ~ element_of_set(U,union_of_members(Vf))
    | element_of_collection(f1(Vf,U),Vf) ) ).

cnf(union_of_members_3,axiom,
    ( element_of_set(U,union_of_members(Vf))
    | ~ element_of_set(U,Uu1)
    | ~ element_of_collection(Uu1,Vf) ) ).

%----Pi (intersection of members).
cnf(intersection_of_members_4,axiom,
    ( ~ element_of_set(U,intersection_of_members(Vf))
    | ~ element_of_collection(Va,Vf)
    | element_of_set(U,Va) ) ).

cnf(intersection_of_members_5,axiom,
    ( element_of_set(U,intersection_of_members(Vf))
    | element_of_collection(f2(Vf,U),Vf) ) ).

cnf(intersection_of_members_6,axiom,
    ( element_of_set(U,intersection_of_members(Vf))
    | ~ element_of_set(U,f2(Vf,U)) ) ).

%----Topological space
cnf(topological_space_7,axiom,
    ( ~ topological_space(X,Vt)
    | equal_sets(union_of_members(Vt),X) ) ).

cnf(topological_space_8,axiom,
    ( ~ topological_space(X,Vt)
    | element_of_collection(empty_set,Vt) ) ).

cnf(topological_space_9,axiom,
    ( ~ topological_space(X,Vt)
    | element_of_collection(X,Vt) ) ).

cnf(topological_space_10,axiom,
    ( ~ topological_space(X,Vt)
    | ~ element_of_collection(Y,Vt)
    | ~ element_of_collection(Z,Vt)
    | element_of_collection(intersection_of_sets(Y,Z),Vt) ) ).

cnf(topological_space_11,axiom,
    ( ~ topological_space(X,Vt)
    | ~ subset_collections(Vf,Vt)
    | element_of_collection(union_of_members(Vf),Vt) ) ).

cnf(topological_space_12,axiom,
    ( topological_space(X,Vt)
    | ~ equal_sets(union_of_members(Vt),X)
    | ~ element_of_collection(empty_set,Vt)
    | ~ element_of_collection(X,Vt)
    | element_of_collection(f3(X,Vt),Vt)
    | subset_collections(f5(X,Vt),Vt) ) ).

cnf(topological_space_13,axiom,
    ( topological_space(X,Vt)
    | ~ equal_sets(union_of_members(Vt),X)
    | ~ element_of_collection(empty_set,Vt)
    | ~ element_of_collection(X,Vt)
    | element_of_collection(f3(X,Vt),Vt)
    | ~ element_of_collection(union_of_members(f5(X,Vt)),Vt) ) ).

cnf(topological_space_14,axiom,
    ( topological_space(X,Vt)
    | ~ equal_sets(union_of_members(Vt),X)
    | ~ element_of_collection(empty_set,Vt)
    | ~ element_of_collection(X,Vt)
    | element_of_collection(f4(X,Vt),Vt)
    | subset_collections(f5(X,Vt),Vt) ) ).

cnf(topological_space_15,axiom,
    ( topological_space(X,Vt)
    | ~ equal_sets(union_of_members(Vt),X)
    | ~ element_of_collection(empty_set,Vt)
    | ~ element_of_collection(X,Vt)
    | element_of_collection(f4(X,Vt),Vt)
    | ~ element_of_collection(union_of_members(f5(X,Vt)),Vt) ) ).

cnf(topological_space_16,axiom,
    ( topological_space(X,Vt)
    | ~ equal_sets(union_of_members(Vt),X)
    | ~ element_of_collection(empty_set,Vt)
    | ~ element_of_collection(X,Vt)
    | ~ element_of_collection(intersection_of_sets(f3(X,Vt),f4(X,Vt)),Vt)
    | subset_collections(f5(X,Vt),Vt) ) ).

cnf(topological_space_17,axiom,
    ( topological_space(X,Vt)
    | ~ equal_sets(union_of_members(Vt),X)
    | ~ element_of_collection(empty_set,Vt)
    | ~ element_of_collection(X,Vt)
    | ~ element_of_collection(intersection_of_sets(f3(X,Vt),f4(X,Vt)),Vt)
    | ~ element_of_collection(union_of_members(f5(X,Vt)),Vt) ) ).

%----Open set
cnf(open_set_18,axiom,
    ( ~ open(U,X,Vt)
    | topological_space(X,Vt) ) ).

cnf(open_set_19,axiom,
    ( ~ open(U,X,Vt)
    | element_of_collection(U,Vt) ) ).

cnf(open_set_20,axiom,
    ( open(U,X,Vt)
    | ~ topological_space(X,Vt)
    | ~ element_of_collection(U,Vt) ) ).

%----Closed set
cnf(closed_set_21,axiom,
    ( ~ closed(U,X,Vt)
    | topological_space(X,Vt) ) ).

cnf(closed_set_22,axiom,
    ( ~ closed(U,X,Vt)
    | open(relative_complement_sets(U,X),X,Vt) ) ).

cnf(closed_set_23,axiom,
    ( closed(U,X,Vt)
    | ~ topological_space(X,Vt)
    | ~ open(relative_complement_sets(U,X),X,Vt) ) ).

%----Finer topology
cnf(finer_topology_24,axiom,
    ( ~ finer(Vt,Vs,X)
    | topological_space(X,Vt) ) ).

cnf(finer_topology_25,axiom,
    ( ~ finer(Vt,Vs,X)
    | topological_space(X,Vs) ) ).

cnf(finer_topology_26,axiom,
    ( ~ finer(Vt,Vs,X)
    | subset_collections(Vs,Vt) ) ).

cnf(finer_topology_27,axiom,
    ( finer(Vt,Vs,X)
    | ~ topological_space(X,Vt)
    | ~ topological_space(X,Vs)
    | ~ subset_collections(Vs,Vt) ) ).

%----Basis for a topology
cnf(basis_for_topology_28,axiom,
    ( ~ basis(X,Vf)
    | equal_sets(union_of_members(Vf),X) ) ).

cnf(basis_for_topology_29,axiom,
    ( ~ basis(X,Vf)
    | ~ element_of_set(Y,X)
    | ~ element_of_collection(Vb1,Vf)
    | ~ element_of_collection(Vb2,Vf)
    | ~ element_of_set(Y,intersection_of_sets(Vb1,Vb2))
    | element_of_set(Y,f6(X,Vf,Y,Vb1,Vb2)) ) ).

cnf(basis_for_topology_30,axiom,
    ( ~ basis(X,Vf)
    | ~ element_of_set(Y,X)
    | ~ element_of_collection(Vb1,Vf)
    | ~ element_of_collection(Vb2,Vf)
    | ~ element_of_set(Y,intersection_of_sets(Vb1,Vb2))
    | element_of_collection(f6(X,Vf,Y,Vb1,Vb2),Vf) ) ).

cnf(basis_for_topology_31,axiom,
    ( ~ basis(X,Vf)
    | ~ element_of_set(Y,X)
    | ~ element_of_collection(Vb1,Vf)
    | ~ element_of_collection(Vb2,Vf)
    | ~ element_of_set(Y,intersection_of_sets(Vb1,Vb2))
    | subset_sets(f6(X,Vf,Y,Vb1,Vb2),intersection_of_sets(Vb1,Vb2)) ) ).

cnf(basis_for_topology_32,axiom,
    ( basis(X,Vf)
    | ~ equal_sets(union_of_members(Vf),X)
    | element_of_set(f7(X,Vf),X) ) ).

cnf(basis_for_topology_33,axiom,
    ( basis(X,Vf)
    | ~ equal_sets(union_of_members(Vf),X)
    | element_of_collection(f8(X,Vf),Vf) ) ).

cnf(basis_for_topology_34,axiom,
    ( basis(X,Vf)
    | ~ equal_sets(union_of_members(Vf),X)
    | element_of_collection(f9(X,Vf),Vf) ) ).

cnf(basis_for_topology_35,axiom,
    ( basis(X,Vf)
    | ~ equal_sets(union_of_members(Vf),X)
    | element_of_set(f7(X,Vf),intersection_of_sets(f8(X,Vf),f9(X,Vf))) ) ).

cnf(basis_for_topology_36,axiom,
    ( basis(X,Vf)
    | ~ equal_sets(union_of_members(Vf),X)
    | ~ element_of_set(f7(X,Vf),Uu9)
    | ~ element_of_collection(Uu9,Vf)
    | ~ subset_sets(Uu9,intersection_of_sets(f8(X,Vf),f9(X,Vf))) ) ).

%----Topology generated by a basis
cnf(topology_generated_37,axiom,
    ( ~ element_of_collection(U,top_of_basis(Vf))
    | ~ element_of_set(X,U)
    | element_of_set(X,f10(Vf,U,X)) ) ).

cnf(topology_generated_38,axiom,
    ( ~ element_of_collection(U,top_of_basis(Vf))
    | ~ element_of_set(X,U)
    | element_of_collection(f10(Vf,U,X),Vf) ) ).

cnf(topology_generated_39,axiom,
    ( ~ element_of_collection(U,top_of_basis(Vf))
    | ~ element_of_set(X,U)
    | subset_sets(f10(Vf,U,X),U) ) ).

cnf(topology_generated_40,axiom,
    ( element_of_collection(U,top_of_basis(Vf))
    | element_of_set(f11(Vf,U),U) ) ).

cnf(topology_generated_41,axiom,
    ( element_of_collection(U,top_of_basis(Vf))
    | ~ element_of_set(f11(Vf,U),Uu11)
    | ~ element_of_collection(Uu11,Vf)
    | ~ subset_sets(Uu11,U) ) ).

%----Subspace topology
cnf(subspace_topology_42,axiom,
    ( ~ element_of_collection(U,subspace_topology(X,Vt,Y))
    | topological_space(X,Vt) ) ).

cnf(subspace_topology_43,axiom,
    ( ~ element_of_collection(U,subspace_topology(X,Vt,Y))
    | subset_sets(Y,X) ) ).

cnf(subspace_topology_44,axiom,
    ( ~ element_of_collection(U,subspace_topology(X,Vt,Y))
    | element_of_collection(f12(X,Vt,Y,U),Vt) ) ).

cnf(subspace_topology_45,axiom,
    ( ~ element_of_collection(U,subspace_topology(X,Vt,Y))
    | equal_sets(U,intersection_of_sets(Y,f12(X,Vt,Y,U))) ) ).

cnf(subspace_topology_46,axiom,
    ( element_of_collection(U,subspace_topology(X,Vt,Y))
    | ~ topological_space(X,Vt)
    | ~ subset_sets(Y,X)
    | ~ element_of_collection(Uu12,Vt)
    | ~ equal_sets(U,intersection_of_sets(Y,Uu12)) ) ).

%----Interior of a set
cnf(interior_47,axiom,
    ( ~ element_of_set(U,interior(Y,X,Vt))
    | topological_space(X,Vt) ) ).

cnf(interior_48,axiom,
    ( ~ element_of_set(U,interior(Y,X,Vt))
    | subset_sets(Y,X) ) ).

cnf(interior_49,axiom,
    ( ~ element_of_set(U,interior(Y,X,Vt))
    | element_of_set(U,f13(Y,X,Vt,U)) ) ).

cnf(interior_50,axiom,
    ( ~ element_of_set(U,interior(Y,X,Vt))
    | subset_sets(f13(Y,X,Vt,U),Y) ) ).

cnf(interior_51,axiom,
    ( ~ element_of_set(U,interior(Y,X,Vt))
    | open(f13(Y,X,Vt,U),X,Vt) ) ).

cnf(interior_52,axiom,
    ( element_of_set(U,interior(Y,X,Vt))
    | ~ topological_space(X,Vt)
    | ~ subset_sets(Y,X)
    | ~ element_of_set(U,Uu13)
    | ~ subset_sets(Uu13,Y)
    | ~ open(Uu13,X,Vt) ) ).

%----Closure of a set
cnf(closure_53,axiom,
    ( ~ element_of_set(U,closure(Y,X,Vt))
    | topological_space(X,Vt) ) ).

cnf(closure_54,axiom,
    ( ~ element_of_set(U,closure(Y,X,Vt))
    | subset_sets(Y,X) ) ).

cnf(closure_55,axiom,
    ( ~ element_of_set(U,closure(Y,X,Vt))
    | ~ subset_sets(Y,V)
    | ~ closed(V,X,Vt)
    | element_of_set(U,V) ) ).

cnf(closure_56,axiom,
    ( element_of_set(U,closure(Y,X,Vt))
    | ~ topological_space(X,Vt)
    | ~ subset_sets(Y,X)
    | subset_sets(Y,f14(Y,X,Vt,U)) ) ).

cnf(closure_57,axiom,
    ( element_of_set(U,closure(Y,X,Vt))
    | ~ topological_space(X,Vt)
    | ~ subset_sets(Y,X)
    | closed(f14(Y,X,Vt,U),X,Vt) ) ).

cnf(closure_58,axiom,
    ( element_of_set(U,closure(Y,X,Vt))
    | ~ topological_space(X,Vt)
    | ~ subset_sets(Y,X)
    | ~ element_of_set(U,f14(Y,X,Vt,U)) ) ).

%----Neighborhood
cnf(neighborhood_59,axiom,
    ( ~ neighborhood(U,Y,X,Vt)
    | topological_space(X,Vt) ) ).

cnf(neighborhood_60,axiom,
    ( ~ neighborhood(U,Y,X,Vt)
    | open(U,X,Vt) ) ).

cnf(neighborhood_61,axiom,
    ( ~ neighborhood(U,Y,X,Vt)
    | element_of_set(Y,U) ) ).

cnf(neighborhood_62,axiom,
    ( neighborhood(U,Y,X,Vt)
    | ~ topological_space(X,Vt)
    | ~ open(U,X,Vt)
    | ~ element_of_set(Y,U) ) ).

%----Limit point
cnf(limit_point_63,axiom,
    ( ~ limit_point(Z,Y,X,Vt)
    | topological_space(X,Vt) ) ).

cnf(limit_point_64,axiom,
    ( ~ limit_point(Z,Y,X,Vt)
    | subset_sets(Y,X) ) ).

cnf(limit_point_65,axiom,
    ( ~ limit_point(Z,Y,X,Vt)
    | ~ neighborhood(U,Z,X,Vt)
    | element_of_set(f15(Z,Y,X,Vt,U),intersection_of_sets(U,Y)) ) ).

cnf(limit_point_66,axiom,
    ( ~ limit_point(Z,Y,X,Vt)
    | ~ neighborhood(U,Z,X,Vt)
    | ~ eq_p(f15(Z,Y,X,Vt,U),Z) ) ).

cnf(limit_point_67,axiom,
    ( limit_point(Z,Y,X,Vt)
    | ~ topological_space(X,Vt)
    | ~ subset_sets(Y,X)
    | neighborhood(f16(Z,Y,X,Vt),Z,X,Vt) ) ).

cnf(limit_point_68,axiom,
    ( limit_point(Z,Y,X,Vt)
    | ~ topological_space(X,Vt)
    | ~ subset_sets(Y,X)
    | ~ element_of_set(Uu16,intersection_of_sets(f16(Z,Y,X,Vt),Y))
    | eq_p(Uu16,Z) ) ).

%----Boundary of a set
cnf(boundary_69,axiom,
    ( ~ element_of_set(U,boundary(Y,X,Vt))
    | topological_space(X,Vt) ) ).

cnf(boundary_70,axiom,
    ( ~ element_of_set(U,boundary(Y,X,Vt))
    | element_of_set(U,closure(Y,X,Vt)) ) ).

cnf(boundary_71,axiom,
    ( ~ element_of_set(U,boundary(Y,X,Vt))
    | element_of_set(U,closure(relative_complement_sets(Y,X),X,Vt)) ) ).

cnf(boundary_72,axiom,
    ( element_of_set(U,boundary(Y,X,Vt))
    | ~ topological_space(X,Vt)
    | ~ element_of_set(U,closure(Y,X,Vt))
    | ~ element_of_set(U,closure(relative_complement_sets(Y,X),X,Vt)) ) ).

%----Hausdorff space
cnf(hausdorff_73,axiom,
    ( ~ hausdorff(X,Vt)
    | topological_space(X,Vt) ) ).

cnf(hausdorff_74,axiom,
    ( ~ hausdorff(X,Vt)
    | ~ element_of_set(X_1,X)
    | ~ element_of_set(X_2,X)
    | eq_p(X_1,X_2)
    | neighborhood(f17(X,Vt,X_1,X_2),X_1,X,Vt) ) ).

cnf(hausdorff_75,axiom,
    ( ~ hausdorff(X,Vt)
    | ~ element_of_set(X_1,X)
    | ~ element_of_set(X_2,X)
    | eq_p(X_1,X_2)
    | neighborhood(f18(X,Vt,X_1,X_2),X_2,X,Vt) ) ).

cnf(hausdorff_76,axiom,
    ( ~ hausdorff(X,Vt)
    | ~ element_of_set(X_1,X)
    | ~ element_of_set(X_2,X)
    | eq_p(X_1,X_2)
    | disjoint_s(f17(X,Vt,X_1,X_2),f18(X,Vt,X_1,X_2)) ) ).

cnf(hausdorff_77,axiom,
    ( hausdorff(X,Vt)
    | ~ topological_space(X,Vt)
    | element_of_set(f19(X,Vt),X) ) ).

cnf(hausdorff_78,axiom,
    ( hausdorff(X,Vt)
    | ~ topological_space(X,Vt)
    | element_of_set(f20(X,Vt),X) ) ).

cnf(hausdorff_79,axiom,
    ( hausdorff(X,Vt)
    | ~ topological_space(X,Vt)
    | ~ eq_p(f19(X,Vt),f20(X,Vt)) ) ).

cnf(hausdorff_80,axiom,
    ( hausdorff(X,Vt)
    | ~ topological_space(X,Vt)
    | ~ neighborhood(Uu19,f19(X,Vt),X,Vt)
    | ~ neighborhood(Uu20,f20(X,Vt),X,Vt)
    | ~ disjoint_s(Uu19,Uu20) ) ).

%----Separation in a topological space
cnf(separation_81,axiom,
    ( ~ separation(Va1,Va2,X,Vt)
    | topological_space(X,Vt) ) ).

cnf(separation_82,axiom,
    ( ~ separation(Va1,Va2,X,Vt)
    | ~ equal_sets(Va1,empty_set) ) ).

cnf(separation_83,axiom,
    ( ~ separation(Va1,Va2,X,Vt)
    | ~ equal_sets(Va2,empty_set) ) ).

cnf(separation_84,axiom,
    ( ~ separation(Va1,Va2,X,Vt)
    | element_of_collection(Va1,Vt) ) ).

cnf(separation_85,axiom,
    ( ~ separation(Va1,Va2,X,Vt)
    | element_of_collection(Va2,Vt) ) ).

cnf(separation_86,axiom,
    ( ~ separation(Va1,Va2,X,Vt)
    | equal_sets(union_of_sets(Va1,Va2),X) ) ).

cnf(separation_87,axiom,
    ( ~ separation(Va1,Va2,X,Vt)
    | disjoint_s(Va1,Va2) ) ).

cnf(separation_88,axiom,
    ( separation(Va1,Va2,X,Vt)
    | ~ topological_space(X,Vt)
    | equal_sets(Va1,empty_set)
    | equal_sets(Va2,empty_set)
    | ~ element_of_collection(Va1,Vt)
    | ~ element_of_collection(Va2,Vt)
    | ~ equal_sets(union_of_sets(Va1,Va2),X)
    | ~ disjoint_s(Va1,Va2) ) ).

%----Connected topological space
cnf(connected_space_89,axiom,
    ( ~ connected_space(X,Vt)
    | topological_space(X,Vt) ) ).

cnf(connected_space_90,axiom,
    ( ~ connected_space(X,Vt)
    | ~ separation(Va1,Va2,X,Vt) ) ).

cnf(connected_space_91,axiom,
    ( connected_space(X,Vt)
    | ~ topological_space(X,Vt)
    | separation(f21(X,Vt),f22(X,Vt),X,Vt) ) ).

%----Connected set
cnf(connected_set_92,axiom,
    ( ~ connected_set(Va,X,Vt)
    | topological_space(X,Vt) ) ).

cnf(connected_set_93,axiom,
    ( ~ connected_set(Va,X,Vt)
    | subset_sets(Va,X) ) ).

cnf(connected_set_94,axiom,
    ( ~ connected_set(Va,X,Vt)
    | connected_space(Va,subspace_topology(X,Vt,Va)) ) ).

cnf(connected_set_95,axiom,
    ( connected_set(Va,X,Vt)
    | ~ topological_space(X,Vt)
    | ~ subset_sets(Va,X)
    | ~ connected_space(Va,subspace_topology(X,Vt,Va)) ) ).

%----Open covering
cnf(open_covering_96,axiom,
    ( ~ open_covering(Vf,X,Vt)
    | topological_space(X,Vt) ) ).

cnf(open_covering_97,axiom,
    ( ~ open_covering(Vf,X,Vt)
    | subset_collections(Vf,Vt) ) ).

cnf(open_covering_98,axiom,
    ( ~ open_covering(Vf,X,Vt)
    | equal_sets(union_of_members(Vf),X) ) ).

cnf(open_covering_99,axiom,
    ( open_covering(Vf,X,Vt)
    | ~ topological_space(X,Vt)
    | ~ subset_collections(Vf,Vt)
    | ~ equal_sets(union_of_members(Vf),X) ) ).

%----Compact topological space
cnf(compact_space_100,axiom,
    ( ~ compact_space(X,Vt)
    | topological_space(X,Vt) ) ).

cnf(compact_space_101,axiom,
    ( ~ compact_space(X,Vt)
    | ~ open_covering(Vf1,X,Vt)
    | finite(f23(X,Vt,Vf1)) ) ).

cnf(compact_space_102,axiom,
    ( ~ compact_space(X,Vt)
    | ~ open_covering(Vf1,X,Vt)
    | subset_collections(f23(X,Vt,Vf1),Vf1) ) ).

cnf(compact_space_103,axiom,
    ( ~ compact_space(X,Vt)
    | ~ open_covering(Vf1,X,Vt)
    | open_covering(f23(X,Vt,Vf1),X,Vt) ) ).

cnf(compact_space_104,axiom,
    ( compact_space(X,Vt)
    | ~ topological_space(X,Vt)
    | open_covering(f24(X,Vt),X,Vt) ) ).

cnf(compact_space_105,axiom,
    ( compact_space(X,Vt)
    | ~ topological_space(X,Vt)
    | ~ finite(Uu24)
    | ~ subset_collections(Uu24,f24(X,Vt))
    | ~ open_covering(Uu24,X,Vt) ) ).

%----Compact set
cnf(compact_set_106,axiom,
    ( ~ compact_set(Va,X,Vt)
    | topological_space(X,Vt) ) ).

cnf(compact_set_107,axiom,
    ( ~ compact_set(Va,X,Vt)
    | subset_sets(Va,X) ) ).

cnf(compact_set_108,axiom,
    ( ~ compact_set(Va,X,Vt)
    | compact_space(Va,subspace_topology(X,Vt,Va)) ) ).

cnf(compact_set_109,axiom,
    ( compact_set(Va,X,Vt)
    | ~ topological_space(X,Vt)
    | ~ subset_sets(Va,X)
    | ~ compact_space(Va,subspace_topology(X,Vt,Va)) ) ).

%--------------------------------------------------------------------------
