%--------------------------------------------------------------------------
% File     : LAT003-0 : TPTP v8.1.2. Bugfixed v2.2.1.
% Domain   : Lattice Theory (Ortholattices)
% Axioms   : Ortholattice theory (equality) axioms
% Version  : [McC98b] (equality) axioms.
% English  :

% Refs     : [McC98a] McCune (1998), Automatic Proofs and Counterexamples f
%          : [McC98b] McCune (1998), Email to G. Sutcliffe
% Source   : [McC98b]
% Names    :

% Status   : Satisfiable
% Syntax   : Number of clauses     :   10 (  10 unt;   0 nHn;   0 RR)
%            Number of literals    :   10 (  10 equ;   0 neg)
%            Maximal clause size   :    1 (   1 avg)
%            Maximal term depth    :    4 (   2 avg)
%            Number of predicates  :    1 (   0 usr;   0 prp; 2-2 aty)
%            Number of functors    :    5 (   5 usr;   2 con; 0-2 aty)
%            Number of variables   :   19 (   2 sgn)
% SPC      : 

% Comments :
% Bugfixes : v2.2.1 - Added clauses top and bottom.
%--------------------------------------------------------------------------
%----Axioms for an Ortholattice:
cnf(top,axiom,
    join(complement(X),X) = n1 ).

cnf(bottom,axiom,
    meet(complement(X),X) = n0 ).

cnf(absorption2,axiom,
    join(X,meet(X,Y)) = X ).

cnf(commutativity_of_meet,axiom,
    meet(X,Y) = meet(Y,X) ).

cnf(commutativity_of_join,axiom,
    join(X,Y) = join(Y,X) ).

cnf(associativity_of_meet,axiom,
    meet(meet(X,Y),Z) = meet(X,meet(Y,Z)) ).

cnf(associativity_of_join,axiom,
    join(join(X,Y),Z) = join(X,join(Y,Z)) ).

cnf(complement_involution,axiom,
    complement(complement(X)) = X ).

cnf(join_complement,axiom,
    join(X,join(Y,complement(Y))) = join(Y,complement(Y)) ).

cnf(meet_complement,axiom,
    meet(X,Y) = complement(join(complement(X),complement(Y))) ).

%--------------------------------------------------------------------------
